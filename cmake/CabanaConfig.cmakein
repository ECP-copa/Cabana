############################################################################
# Copyright (c) 2018-2021 by the Cabana authors                            #
# All rights reserved.                                                     #
#                                                                          #
# This file is part of the Cabana library. Cabana is distributed under a   #
# BSD 3-clause license. For the licensing terms see the LICENSE file in    #
# the top-level directory.                                                 #
#                                                                          #
# SPDX-License-Identifier: BSD-3-Clause                                    #
############################################################################

include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}" )
list(APPEND CMAKE_PREFIX_PATH @CMAKE_PREFIX_PATH@)
find_dependency(Kokkos REQUIRED)
set(Cabana_ENABLE_MPI @Cabana_ENABLE_MPI@)
if(Cabana_ENABLE_MPI)
  find_dependency(MPI REQUIRED CXX)
endif()
set(Cabana_ENABLE_ARBORX @Cabana_ENABLE_ARBORX@)
if(Cabana_ENABLE_ARBORX)
  find_dependency(ArborX REQUIRED)
endif()
set(Cabana_ENABLE_CAJITA @Cabana_ENABLE_CAJITA@)
include("${CMAKE_CURRENT_LIST_DIR}/Cabana_Targets.cmake")
if(Cabana_ENABLE_CAJITA)
  include("${CMAKE_CURRENT_LIST_DIR}/CajitaTargets.cmake")
endif()
set(Cabana_ENABLE_HYPRE @Cabana_ENABLE_HYPRE@)
if(Cabana_ENABLE_HYPRE)
  # FIXME: HYPRE workaround. Hypre is currently not exporting the find_package(
  # MPI ) to find the MPI::MPI_C targets and therefore we add this here for now
  # to ensure those targets are found for HYPRE:
  # https://github.com/hypre-space/hypre/pull/423
  enable_language( C )
  find_dependency(HYPRE REQUIRED VERSION @HYPRE_VERSION@)
endif()
set(Cabana_ENABLE_HEFFTE @Cabana_ENABLE_HEFFTE@)
if(Cabana_ENABLE_HEFFTE)
  find_dependency(Heffte REQUIRED VERSION @Heffte_VERSION@)
endif()
